// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel Init
#pragma kernel Emit
#pragma kernel Update

#define THREAD_NUM_X 32

#include "ParticleData.cginc"
//#include "Assets/VectorField/Shaders/FlowGrid.cginc"

RWStructuredBuffer<ParticleData> _Particles;
AppendStructuredBuffer<uint> _DeadList;
ConsumeStructuredBuffer<uint> _ParticlePool;
AppendStructuredBuffer<uint> _ActiveList;
StructuredBuffer<float2> _EmitBuffer;

int _EmitCount;
float _DT;
float _Time;
float _LifeTime;
float _ScaleMin;
float _ScaleMax;

// 乱数生成
inline float rnd(float2 p) {
	return frac(sin(dot(p, float2(12.9898, 78.233))) * 43758.5453);
}

inline float3 rnd3(float2 p) {
	return 2.0 * (float3(rnd(p * 1), rnd(p * 2), rnd(p * 3)) - 0.5);
}

float3 hsv_to_rgb(float3 HSV)
{
	float4 k = float4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
	float3 p = abs(frac(HSV.xxx + k.xyz) * 6.0 - k.www);
	return HSV.z * lerp(k.xxx, clamp(p - k.xxx, 0.0, 1.0), HSV.y);
}

[numthreads(THREAD_NUM_X, 1, 1)]
void Init(uint3 id : SV_DispatchThreadID)
{
	uint no = id.x;

	_Particles[no].isActive = false;
	_DeadList.Append(no);
}

[numthreads(THREAD_NUM_X, 1, 1)]
void Emit(uint3 id : SV_DispatchThreadID)
{
	uint idx = id.x;
	if (idx >= _EmitCount) return;

	uint no = _ParticlePool.Consume();

	float2 seed = float2(no + _Time, no + 1.583 + _Time);

	float2 pos = _EmitBuffer[idx];

	_Particles[no].isActive = true;
	_Particles[no].position = pos;
	_Particles[no].radius = 0.0;
	_Particles[no].maxRadius = _ScaleMin + (rnd(seed + 3.15)) * (_ScaleMax - _ScaleMin);
	_Particles[no].duration = _LifeTime;
}

[numthreads(THREAD_NUM_X, 1, 1)]
void Update(uint3 id : SV_DispatchThreadID)
{
	uint no = id.x;

	if (_Particles[no].isActive) {
		_Particles[no].duration -= _DT;
		_Particles[no].radius = lerp(0, _Particles[no].maxRadius, 1.0 - max(_Particles[no].duration / _LifeTime, 0));
		if (_Particles[no].duration <= 0) {
			_Particles[no].isActive = false;
			_DeadList.Append(no);
		}
		else {
			_ActiveList.Append(no);
		}
	}

}

